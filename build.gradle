plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'moe.hayden'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    sqliteJdbcVersion = '3.36.0.3'

    junitVersion = '5.8.2'
    testfxVersion = '4.0.16'
    hamcrestVersion = '2.2'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'moe.hayden.votebox'
    mainClass = 'moe.hayden.votebox.VoteboxApplication'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation("org.xerial:sqlite-jdbc:${sqliteJdbcVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testImplementation("org.testfx:testfx-junit5:${testfxVersion}-alpha")
    testImplementation("org.hamcrest:hamcrest:${hamcrestVersion}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}
